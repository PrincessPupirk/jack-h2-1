class Score {
    field int _levelId;  // Номер текущего уровня
    field int _coins;    // Количество собранных монет
    field int _keys;     // Количество собранных ключей

    /** Создает новый счетчик для указанного уровня */
    constructor Score new(int levelId) {
        let _coins = 0;
        let _keys = 0;
        let _levelId = levelId;
        return this;
    }

    method void incrementCoins() {
        let _coins = _coins + 1;
        do render();
        return;
    }

    method void incrementKeys() {
        let _keys = _keys + 1;
        do render();
        return;
    }

    method void decrementKeys() {
        let _keys = _keys - 1;
        do render();
        return;
    }

    /** Проверяет, есть ли у игрока ключ */
    method boolean hasKey() {
        return _keys > 0;
    }

    /** Отображает текущий счет на экране */
    method void render() {
        do Output.moveCursor(0, 0);
        do Output.printString("Level ");
        do Output.printInt(_levelId);

        do Output.moveCursor(0, 15);
        do Output.printString("Coins: ");
        do Output.printInt(_coins);
        
        do Output.moveCursor(0, 31);
        do Output.printString("Keys: ");
        do Output.printInt(_keys);
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}
