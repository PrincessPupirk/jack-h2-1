/** Класс, отвечающий за типы блоков. */
class Block {
    static Block air;
    static Block wall;
    static Block coin;
    static Block key;
    static Block finish;
    static Block lock;
    static Block fakeWall;

    static Block spikesUp;    // шипы вверх
    static Block spikesRight; // шипы вправо
    static Block spikesDown;  // шипы вниз
    static Block spikesLeft;  // шипы влево

    field boolean _moveThrough;

    /** Создает новый блок с параметром проходимости */
    constructor Block new(boolean moveThrough){
        let _moveThrough = moveThrough;
        return this;
    }

    /** Инициализирует все типы блоков */
    function void initBlocks(){
        let air = Block.new(true);      // пустота - проходимый
        let wall = Block.new(false);    // стена - непроходимая
        let coin = Block.new(true);     // монета - проходимая
        let key = Block.new(true);      // ключ - проходимый
        let finish = Block.new(true);   // финиш - проходимый
        let lock = Block.new(false);    // замок - непроходимый
        let fakeWall = Block.new(true); // поддельная стена - проходимый

        let spikesUp = Block.new(true);    // шипы - проходимые
        let spikesRight = Block.new(true);
        let spikesDown = Block.new(true);
        let spikesLeft = Block.new(true);
        return;
    }

    /** Преобразует символ в блок на карте */
    function Block parse(char blockChar){
        if ((blockChar = 32) | (blockChar = 80)){ // ' ' или 'P' = воздух
            return air;
        }

        if (blockChar = 35){ // '#' = стена
            return wall;
        }

        if (blockChar = 67){ // 'C' = монета
            return coin;
        }

        if (blockChar = 75){ // 'K' = ключ
            return key;
        }

        if (blockChar = 70){ // 'F' = финиш
            return finish;
        }

        if (blockChar = 76){ // 'L' = замок
            return lock;
        }

        if (blockChar = 79){ // 'O' = фейк стена
            return fakeWall;
        }

        if (blockChar = 77){ // 'M' = шипы вверх
            return spikesUp;
        }

        if (blockChar = 69){ // 'E' = шипы вправо
            return spikesRight;
        }

        if (blockChar = 87){ // 'W' = шипы вниз
            return spikesDown;
        }

        if (blockChar = 84){ // 'T' = шипы влево
            return spikesLeft;
        }

        return 0;
    }

    /** 
    * Обрабатывает попытку движения игрока через блок
    * Возвращает true, если движение возможно
    */
    method boolean onTryMove(Level level, Map map, int x, int y){
        var Score score;

        if (this = lock){
            let score = level.getScore();
            if (score.hasKey()){
                do score.decrementKeys();
                do map.removeBlock(x, y);
                return true;
            }
        }

        return false;
    }

    /** 
    * Обрабатывает нахождение игрока на блоке 
    * Note: чтобы на каждом блоке не было получения Score, код дублирован в тело нужных IF
    */
    method void onStepped(Level level, Map map, int x, int y){
        var Score score;

        if (this = coin){
            let score = level.getScore();
            do score.incrementCoins();
            do map.removeBlock(x, y);
            return;
        }

        if (this = key){
            let score = level.getScore();
            do score.incrementKeys();
            do map.removeBlock(x, y);
            return;
        }

        if (this = finish){
            do Main.nextLevel();
            return;
        }

        if ((this = spikesUp) | (this = spikesDown) | (this = spikesLeft) | (this = spikesRight)){
            do Main.setDead();
            return;
        }

        return;
    }

    /** Возвращает true, если через блок можно пройти */
    method boolean canMoveThrough(){
        return _moveThrough;
    }

    function Block getAir(){
        return air;
    }

    /** Рисует блок на экране (по координатам) */
    method void render(int x, int y){
        var int memAddress;

        let memAddress = 16384 + (y * 32) + (x / 16);

        if ((this = wall) | (this = fakeWall)){
            do _renderWall(memAddress);
            return;
        }
        if (this = coin){
            do _renderCoin(memAddress);
            return;
        }
        if (this = key){
            do _renderKey(memAddress);
            return;
        }
        if (this = finish){
            do _renderFinish(memAddress);
            return;
        }
        if (this = lock){
            do _renderLock(memAddress);
            return;
        }
        if (this = spikesUp){
            do _renderSpikesUp(memAddress);
            return;
        }
        if (this = spikesRight){
            do _renderSpikesRight(memAddress);
            return;
        }
        if (this = spikesDown){
            do _renderSpikesDown(memAddress);
            return;
        }
        if (this = spikesLeft){
            do _renderSpikesLeft(memAddress);
            return;
        }

        return;
    }

    method void _renderWall(int memAddress){
        do Memory.poke(memAddress + 0, -1);
        do Memory.poke(memAddress + 32, -1);
        do Memory.poke(memAddress + 64, -1);
        do Memory.poke(memAddress + 96, -1);
        do Memory.poke(memAddress + 128, -1);
        do Memory.poke(memAddress + 160, -1);
        do Memory.poke(memAddress + 192, -1);
        do Memory.poke(memAddress + 224, -1);
        do Memory.poke(memAddress + 256, -1);
        do Memory.poke(memAddress + 288, -1);
        do Memory.poke(memAddress + 320, -1);
        do Memory.poke(memAddress + 352, -1);
        do Memory.poke(memAddress + 384, -1);
        do Memory.poke(memAddress + 416, -1);
        do Memory.poke(memAddress + 448, -1);
        do Memory.poke(memAddress + 480, -1);
        return;
    }

    method void _renderCoin(int memAddress){
        do Memory.poke(memAddress + 0, 0);
        do Memory.poke(memAddress + 32, 960);
        do Memory.poke(memAddress + 64, 4080);
        do Memory.poke(memAddress + 96, 7224);
        do Memory.poke(memAddress + 128, 15324);
        do Memory.poke(memAddress + 160, 12876);
        do Memory.poke(memAddress + 192, 25158);
        do Memory.poke(memAddress + 224, 25542);
        do Memory.poke(memAddress + 256, 24646);
        do Memory.poke(memAddress + 288, 24646);
        do Memory.poke(memAddress + 320, 12364);
        do Memory.poke(memAddress + 352, 14428);
        do Memory.poke(memAddress + 384, 7224);
        do Memory.poke(memAddress + 416, 4080);
        do Memory.poke(memAddress + 448, 960);
        do Memory.poke(memAddress + 480, 0);
        return;
    }

    method void _renderKey(int memAddress){
        do Memory.poke(memAddress + 0, 0);
        do Memory.poke(memAddress + 32, 0);
        do Memory.poke(memAddress + 64, 0);
        do Memory.poke(memAddress + 96, 0);
        do Memory.poke(memAddress + 128, 0);
        do Memory.poke(memAddress + 160, 28);
        do Memory.poke(memAddress + 192, 38);
        do Memory.poke(memAddress + 224, 13814);
        do Memory.poke(memAddress + 256, 32766);
        do Memory.poke(memAddress + 288, 62);
        do Memory.poke(memAddress + 320, 28);
        do Memory.poke(memAddress + 352, 0);
        do Memory.poke(memAddress + 384, 0);
        do Memory.poke(memAddress + 416, 0);
        do Memory.poke(memAddress + 448, 0);
        do Memory.poke(memAddress + 480, 0);
        return;
    }

    method void _renderFinish(int memAddress){
        do Memory.poke(memAddress + 0, 0);
        do Memory.poke(memAddress + 32, 0);
        do Memory.poke(memAddress + 64, 16380);
        do Memory.poke(memAddress + 96, 11468);
        do Memory.poke(memAddress + 128, 11468);
        do Memory.poke(memAddress + 160, 13116);
        do Memory.poke(memAddress + 192, 13116);
        do Memory.poke(memAddress + 224, 11468);
        do Memory.poke(memAddress + 256, 11468);
        do Memory.poke(memAddress + 288, 13116);
        do Memory.poke(memAddress + 320, 13116);
        do Memory.poke(memAddress + 352, 11468);
        do Memory.poke(memAddress + 384, 11468);
        do Memory.poke(memAddress + 416, 16380);
        do Memory.poke(memAddress + 448, 12);
        do Memory.poke(memAddress + 480, 12);
        return;
    }

    method void _renderLock(int memAddress){
        do Memory.poke(memAddress + 0, 0);
        do Memory.poke(memAddress + 32, 2016);
        do Memory.poke(memAddress + 64, 3120);
        do Memory.poke(memAddress + 96, 2448);
        do Memory.poke(memAddress + 128, 2640);
        do Memory.poke(memAddress + 160, 2640);
        do Memory.poke(memAddress + 192, 2640);
        do Memory.poke(memAddress + 224, 2640);
        do Memory.poke(memAddress + 256, 8184);
        do Memory.poke(memAddress + 288, 12300);
        do Memory.poke(memAddress + 320, 8196);
        do Memory.poke(memAddress + 352, 8196);
        do Memory.poke(memAddress + 384, 8196);
        do Memory.poke(memAddress + 416, 8196);
        do Memory.poke(memAddress + 448, 12300);
        do Memory.poke(memAddress + 480, 8184);
        return;
    }

    /** Отрисовка шипов вверх */
    method void _renderSpikesUp(int memAddress){
        do Memory.poke(memAddress + 0, 0);
        do Memory.poke(memAddress + 32, 0);
        do Memory.poke(memAddress + 64, 0);
        do Memory.poke(memAddress + 96, 0);
        do Memory.poke(memAddress + 128, 0);
        do Memory.poke(memAddress + 160, 0);
        do Memory.poke(memAddress + 192, 0);
        do Memory.poke(memAddress + 224, 0);
        do Memory.poke(memAddress + 256, 8456);
        do Memory.poke(memAddress + 288, 8456);
        do Memory.poke(memAddress + 320, 29596);
        do Memory.poke(memAddress + 352, 29596);
        do Memory.poke(memAddress + 384, 32766);
        do Memory.poke(memAddress + 416, -1);
        do Memory.poke(memAddress + 448, -1);
        do Memory.poke(memAddress + 480, -1);
        return;
    }

    /** Отрисовка шипов вправо */
    method void _renderSpikesRight(int memAddress){
        do Memory.poke(memAddress + 0, 7);
        do Memory.poke(memAddress + 32, 15);
        do Memory.poke(memAddress + 64, 63);
        do Memory.poke(memAddress + 96, 255);
        do Memory.poke(memAddress + 128, 63);
        do Memory.poke(memAddress + 160, 15);
        do Memory.poke(memAddress + 192, 15);
        do Memory.poke(memAddress + 224, 63);
        do Memory.poke(memAddress + 256, 255);
        do Memory.poke(memAddress + 288, 63);
        do Memory.poke(memAddress + 320, 15);
        do Memory.poke(memAddress + 352, 15);
        do Memory.poke(memAddress + 384, 63);
        do Memory.poke(memAddress + 416, 255);
        do Memory.poke(memAddress + 448, 63);
        do Memory.poke(memAddress + 480, 7);
        return;
    }

    /** Отрисовка шипов вниз */
    method void _renderSpikesDown(int memAddress){
        do Memory.poke(memAddress + 0, -1);
        do Memory.poke(memAddress + 32, -1);
        do Memory.poke(memAddress + 64, -1);
        do Memory.poke(memAddress + 96, 32766);
        do Memory.poke(memAddress + 128, 14798);
        do Memory.poke(memAddress + 160, 14798);
        do Memory.poke(memAddress + 192, 4228);
        do Memory.poke(memAddress + 224, 4228);
        do Memory.poke(memAddress + 256, 0);
        do Memory.poke(memAddress + 288, 0);
        do Memory.poke(memAddress + 320, 0);
        do Memory.poke(memAddress + 352, 0);
        do Memory.poke(memAddress + 384, 0);
        do Memory.poke(memAddress + 416, 0);
        do Memory.poke(memAddress + 448, 0);
        do Memory.poke(memAddress + 480, 0);
        return;
    }

    /** Отрисовка шипов влево */
    method void _renderSpikesLeft(int memAddress){
        do Memory.poke(memAddress + 0, -8192);
        do Memory.poke(memAddress + 32, -1024);
        do Memory.poke(memAddress + 64, -256);
        do Memory.poke(memAddress + 96, -1024);
        do Memory.poke(memAddress + 128, -4096);
        do Memory.poke(memAddress + 160, -4096);
        do Memory.poke(memAddress + 192, -1024);
        do Memory.poke(memAddress + 224, -256);
        do Memory.poke(memAddress + 256, -1024);
        do Memory.poke(memAddress + 288, -4096);
        do Memory.poke(memAddress + 320, -4096);
        do Memory.poke(memAddress + 352, -1024);
        do Memory.poke(memAddress + 384, -256);
        do Memory.poke(memAddress + 416, -1024);
        do Memory.poke(memAddress + 448, -4096);
        do Memory.poke(memAddress + 480, -8192);
        return;
    }
}
