class Map {
    field Array _blockArray;  // массив блоков карты
    field int _width;         // ширина карты в блоках
    field int _height;        // высота карты в блоках

    /** Создает новую карту с указанными размерами и массивом блоков */
    constructor Map new(Array blockArray, int width, int height){
        let _blockArray = blockArray;
        let _width = width;
        let _height = height;
        return this;
    }

    /** 
    * Возвращает блок по координатам на карте
    * Если координаты вне карты, возвращает -1 
    */
    method Block getBlock(int x, int y){
        if ((x < 0) | (y < 0) | (x + 1 > _width) | (y + 1 > _height)){
            return -1;
        }

        return _blockArray[y * _width + x];
    }

    /** Заменяет блок на указанной позиции на air (пустота) */
    method void removeBlock(int x, int y) {
        let _blockArray[y * _width + x] = Block.getAir();
        return;
    }

    /** Рисует всю карту с указанным смещением и размером блоков */
    method void render(int startX, int startY){
        var Block block;
        var int mapSize;
        var int x;
        var int y;

        while (y < _height){
            while (x < _width){
                let block = _blockArray[y * _width + x];
                do block.render(startX + (x * 16), startY + (y * 16));

                let x = x + 1;
            }

            let x = 0;
            let y = y + 1;
        }

        return;
    }

    /** 
    * Перерисовывает один блок на определенной позиции
    * Используется для оптимизации отрисовки при изменении одного блока
    */
    method void reRender(int startX, int startY, int gameX, int gameY) {
        var int x;
        var int y;
        var Block block;

        let x = startX + (gameX * 16);
        let y = startY + (gameY * 16);

        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + 15, y + 15);
        do Screen.setColor(true);

        let block = getBlock(gameX, gameY);
        if (~(block = -1)) {
            do block.render(x, y);
        }

        return;
    }

    method void dispose() {
        do _blockArray.dispose();
        do Memory.deAlloc(this);
        return;
    }
}
