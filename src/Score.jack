class Score {
    field int _levelId;
    field int _coins;
    field int _keys;

    /** Создает новый счетчик для указанного уровня */
    constructor Score new(int levelId) {
        let _coins = 0;
        let _keys = 0;
        let _levelId = levelId;
        return this;
    }

    /** Отображает текущий счет на экране */
    method void render() {
        do Output.moveCursor(0, 0);
        do Output.printString("Level ");
        do Output.printInt(_levelId);

        do Output.moveCursor(0, 15);
        do Output.printString("Coins: ");
        do Output.printInt(_coins);
        
        do Output.moveCursor(0, 31);
        do Output.printString("Keys: ");
        do Output.printInt(_keys);
        return;
    }

    method void incrementCoins() {
        let _coins = _coins + 1;
        do render();
        return;
    }

    method void incrementKeys() {
        let _keys = _keys + 1;
        do render();
        return;
    }

    method void decrementKeys() {
        let _keys = _keys - 1;
        do render();
        return;
    }

    method void setCoins(int coins) {
        let _coins = coins;
        do render();
        return;
    }

    method int getCoins() {
        return _coins;
    }

    /** Проверяет, есть ли у игрока ключ */
    method boolean hasKey() {
        return _keys > 0;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}
